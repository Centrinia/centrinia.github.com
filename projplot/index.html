<html>
    <head>
        <title>Projective Plane Plotter</title>
        <script type="text/javascript" src="projplot.js"></script>
        <script id="shader-fragment-stereographic" type="x-shader/x-fragment">
            precision mediump float;
            varying vec2 v_position;
            uniform vec4 u_rotation;
            uniform float u_displacement;

            /* Transform a vector using a quaternion. */
            vec3 qtransform(vec4 q, vec3 v) {
                return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
            }
            void main() {
                vec2 XY = v_position*u_displacement / 2.0;
                vec3 p =  vec3(2.0 * XY, 1.0 - dot(XY,XY));

                p /= 1.0 + dot(XY,XY);


                /* Rotate the sphere. */
                p = qtransform(u_rotation,p);

                /* Evaluate the polynomial. */
                float y = f(p);

                /* The color is determined by the sign of the function value. */
                y = y > 0.0 ? 0.65 : 0.15;
                /* Handle the axes. */
                vec4 grid = vec4(0.0, 0.0, 0.0, 1.0);

                float TOL = 3e-2;
                grid.rgb = step(-TOL, -abs(p.yxz));

                vec2 q = p.xy / p.z;
                float TOL2 = 7e-3;
                grid.gr += step(-TOL2, -abs(mod(q.xy+0.5,1.0)-0.5)) * 0.2;


                gl_FragColor = vec4(y, y, y, 1.0) + grid;
            }
        </script>

        <script id="shader-fragment-affine" type="x-shader/x-fragment">
            precision mediump float;
            varying vec2 v_position;
            uniform vec4 u_rotation;
            uniform float u_displacement;

            /* Transform a vector using a quaternion. */
            vec3 qtransform(vec4 q, vec3 v) {
                return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
            }
            void main() {
                /* Choose the intersection that is closer to the viewpoint. Since the 
                    * view vector points at the sphere and the viewpoint is outside it, 
                    * the scalar should be minimized. */
                vec3 p = vec3(v_position*u_displacement, 1.0);
                
                /* Rotate the sphere. */
                p = qtransform(u_rotation,p);

                /* Evaluate the polynomial. */
                float y = f(p);

                /* The color is determined by the sign of the function value. */
                y = y > 0.0 ? 0.65 : 0.15;
                /* Handle the axes. */
                vec4 grid = vec4(0.0, 0.0, 0.0, 1.0);

                float TOL = 3e-2;
                grid.rgb = step(-TOL, -abs(p.yxz));

                vec2 q = p.xy / p.z;
                float TOL2 = 7e-3;
                grid.gr += step(-TOL2, -abs(mod(q.xy+0.5,1.0)-0.5)) * 0.2;


                gl_FragColor = vec4(y, y, y, 1.0) + grid;
            }
        </script>
        <script id="shader-fragment" type="x-shader/x-fragment">
            precision highp float;
            varying vec2 v_position;
            uniform vec4 u_rotation;
            uniform vec3 u_center;
            uniform float u_radius;
            uniform float u_displacement;

            /* Transform a vector using a quaternion. */
            vec3 qtransform(vec4 q, vec3 v) {
                return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
            }
            void main() {
                /* Some point in front of the viewpoint and on the view vector. */
                vec3 viewDest = vec3(v_position, 0.0);
                /* The viewpoint. */
                vec3 viewSource = vec3(0.0, 0.0, u_displacement);
                vec3 viewVector = viewDest - viewSource;

                /* Solve for the intersection. */
                float a = dot(viewVector, viewVector);
                float b = 2.0*dot(viewVector, viewSource-u_center);
                float c = dot(viewSource-u_center, viewSource-u_center)-u_radius*u_radius;
                float disc = b*b-4.0*a*c;
                if(disc >= 0.0) {
                    /* Compute the intersection with the sphere. */
                    float t0 = -b / (2.0*a);
                    /* Choose the intersection that is closer to the viewpoint. Since the 
                     * view vector points at the sphere and the viewpoint is outside it, 
                     * the scalar should be minimized. */
                    float idiscSqrt = inversesqrt(disc);
                    float t1 = abs(1.0 / (2.0*a*idiscSqrt));
                    float t = t0-t1;
                    vec3 p = t*viewVector+viewSource;
                    vec3 p2 = (t0+t1)*viewVector+viewSource;

                    /* Rotate the sphere. */
                    p = qtransform(u_rotation,p);
                    p2 = qtransform(u_rotation,p2);

                    /* Evaluate the polynomial. */
                    float y = f(p);
                    float y2 = f(p2);

                    /* The color is determined by the sign of the function value. */
                    y = y > 0.0 ? 0.65 : 0.15;
                    y2 = y2 > 0.0 ? 0.1 : -0.1;
                    y += y2;
                    /* Handle the axes. */

                    vec4 grid = vec4(0.0, 0.0, 0.0, 1.0);

                    float TOL = 1e-2;
                    grid.rgb = step(-TOL, -abs(p.yxz));
                    grid.rgb += step(-TOL, -abs(p2.yxz)) * 0.05;

                    float TOL2 = 7e-3;
                    vec2 q = p.xy / p.z;
                    grid.gr += step(-TOL2, -abs(mod(q.xy+0.5,1.0)-0.5)) * 0.2;
                    vec2 q2 = p2.xy / p2.z;
                    grid.gr += step(-TOL2, -abs(mod(q2.xy+0.5,1.0)-0.5)) * 0.05;

                    gl_FragColor = vec4(y, y, y, 1.0) + grid;

                } else {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            }
        </script>
        <script id="shader-vertex" type="x-shader/x-vertex">
            attribute vec2 a_position;
            varying vec2 v_position;
            void main() {
                gl_Position = vec4(a_position,0.0,1.0);
                v_position = a_position;
            }
        </script>
    </head>
    <body>
        <canvas id="canvas" width="1024" height="1024"></canvas>
        <p>
            Equation:
            <input type='text' id='equation-text' size='100'/>
            <br/>
            <select id='equation'>
                <option value='x^2=y'>Parabola</option>
                <option value='x^2+y^2=1'>Unit Circle</option>
                <option value='(x-2)*x*(x+1)=y^2'>(x-2)*x*(x+1)=y^2</option>
                <option value='x^3*y+y^3*z+z^3*x=0'>Klein Quartic</option>
                <option value='-((x+2)^2+(y-2)^2-1)*((x-2)^2+(y-2)^2-1)*(1.7*x^2+200*(y+2.5)^4-16)*(x^2+y^2-25)=0'>Face</option>
            </select>
        </p>
        <p>
            Mode: <button id="mode-button">Affine</button>
        </p>
        <p>
            Plane:
            <select id='plane'>
                <option value='xy'>X-Y</option>
                <option value='xz'>X-Z</option>
                <option value='yz'>Y-Z</option>
            </select>
        </p>
        <p>
            Cursor location: <span id='cursor-location'></span>
        </p>
        <p>
            Change of coordinates: <span id='quat'></span>
        </p>
        <p>
            Drag the sphere to change the view. The mouse wheel alters the distance to the center with mouse wheel down moving the viewpoint closer. Switch between projective and affine modes. Select the equation on the dropdown menu. The red line is the x axis, the green line is the y axis, and the blue line is the line at infinity.
        </p>
    </body>
</html>
