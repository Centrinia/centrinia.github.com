<html>
    <head>
        <title>Mandelbrot Orbits On Riemann Sphere</title>
        <script type="text/javascript" src="projplot.js"></script>
        <script id="shader-fragment-stereographic" type="x-shader/x-fragment">
            #define M_PI 3.14159265358979323846264338327950
            precision mediump float;
            varying vec2 v_position;
            uniform vec4 u_rotation;
            uniform vec2 u_julia;
            uniform int u_mode;
            uniform float u_displacement;

            /* Transform a vector using a quaternion. */
            vec3 qtransform(vec4 q, vec3 v) {
                return v + 2.0 * cross(cross(v, q.xyz) + q.w * v, q.xyz);
            }
            /* https://github.com/Jam3/glsl-hsl2rgb/blob/master/index.glsl */
            float hue2rgb(float f1, float f2, float hue) {
                if (hue < 0.0)
                    hue += 1.0;
                else if (hue > 1.0)
                    hue -= 1.0;
                float res;
                if ((6.0 * hue) < 1.0)
                    res = f1 + (f2 - f1) * 6.0 * hue;
                else if ((2.0 * hue) < 1.0)
                    res = f2;
                else if ((3.0 * hue) < 2.0)
                    res = f1 + (f2 - f1) * ((2.0 / 3.0) - hue) * 6.0;
                else
                    res = f1;
                return res;
            }

            vec3 hsl2rgb(vec3 hsl) {
                vec3 rgb;
                
                if (hsl.y == 0.0) {
                    rgb = vec3(hsl.z); // Luminance
                } else {
                    float f2;
                    
                    if (hsl.z < 0.5)
                        f2 = hsl.z * (1.0 + hsl.y);
                    else
                        f2 = hsl.z + hsl.y - hsl.y * hsl.z;
                        
                    float f1 = 2.0 * hsl.z - f2;
                    
                    rgb.r = hue2rgb(f1, f2, hsl.x + (1.0/3.0));
                    rgb.g = hue2rgb(f1, f2, hsl.x);
                    rgb.b = hue2rgb(f1, f2, hsl.x - (1.0/3.0));
                }   
                return rgb;
            }

            vec3 complex_hsl(vec2 c) {
                vec3 hsl;
#if 0
                hsl.x = atan(c.y, c.x) / (2.0*M_PI);
                hsl.z = pow(0.5, length(c));
#else
                hsl.x = mod(atan(c.y, c.x) / (2.0*M_PI) + 0.5, 1.0);
                hsl.z = 1.0 - pow(0.5, length(c));
#endif
                hsl.y = 1.0;
                return hsl;
            }

            vec2 f(vec2 c) {
                vec2 z;
                if(u_mode == 0) {
                    z = vec2(0.0, 0.0);
                    for(int i=0;i<ITERATIONS;i++) {
                        float t = z.y*z.y;
                        z = vec2(z.x * z.x + ISQ_RE * t, 2.0 * z.x * z.y + ISQ_IM * t) + c;
                    }
                } else {
                    z = c;
                    for(int i=0;i<ITERATIONS;i++) {
                        float t = z.y*z.y;
                        z = vec2(z.x * z.x + ISQ_RE * t, 2.0 * z.x * z.y + ISQ_IM * t) + u_julia;
                    }
                }
                return z;
            }
            void main() {
                vec2 XY = v_position*u_displacement / 2.0;
                vec3 p =  vec3(2.0 * XY, 1.0 - dot(XY,XY));

                p /= 1.0 + dot(XY,XY);


                /* Rotate the sphere. */
                p = qtransform(u_rotation,p);

                vec2 z = p.xy / (1.0-p.z);
                vec2 fz = f(z);

                gl_FragColor = vec4(hsl2rgb(complex_hsl(fz)), 1.0);
            }
        </script>

        <script id="shader-vertex" type="x-shader/x-vertex">
            attribute vec2 a_position;
            varying vec2 v_position;
            void main() {
                gl_Position = vec4(a_position,0.0,1.0);
                v_position = a_position;
            }
        </script>
    </head>
    <body>
        <canvas id="canvas" width="1024" height="1024"></canvas>
        <p>
        Iterations: <input type="number" min="1" max="1024" id="iterations" value="16"></input>
        </p>
        <p>
        i^2: 
        <input type="number" id="isq_re" value="-1" step="0.01"></input> + i * 
        <input type="number" id="isq_im" value="0" step="0.01"></input>
        </p>
        <span>Double click on a point on the Mandelbrot set to toggle to the respective Julia set. Double click on a Julia set to toggle back to the Mandelbrot set.</span>
    </body>
</html>
